"node"

BUILD_TMPL = """\
# GENERATED BY node_archive.bzl
load("@distroless//private/pkg:debian_spdx.bzl", "debian_spdx")
load("@distroless//private/util:merge_providers.bzl", "merge_providers")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "data",
    srcs = glob(
        [
            "output/bin/node",
            "output/LICENSE",
        ],
    ),
    package_dir = "/nodejs",
    strip_prefix = "external/{canonical_name}/output"
)

pkg_tar(
    name = "_control",
    srcs = ["control"]
)

debian_spdx(
    name = "spdx",
    control = ":_control.tar",
    data = ":data.tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)

merge_providers(
    name = "{name}",
    srcs = [":data", ":spdx"],
    visibility = ["//visibility:public"],
)
"""

def _impl(rctx):
    rctx.report_progress("Fetching {}".format(rctx.attr.package_name))
    rctx.download_and_extract(
        url = rctx.attr.urls,
        sha256 = rctx.attr.sha256,
        type = rctx.attr.type,
        stripPrefix = rctx.attr.strip_prefix,
        output = "output",
    )
    rctx.template(
        "control",
        rctx.attr.control,
        substitutions = {
            "{{VERSION}}": rctx.attr.version,
            "{{ARCHITECTURE}}": rctx.attr.architecture,
            "{{SHA256}}": rctx.attr.sha256,
        },
    )
    rctx.file(
        "BUILD.bazel",
        content = BUILD_TMPL.format(
            canonical_name = rctx.attr.name,
            name = rctx.attr.name.split("~")[-1],
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        ),
    )

node_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "type": attr.string(default = ".tar.gz"),
        "strip_prefix": attr.string(),
        "package_name": attr.string(default = "nodejs"),
        "version": attr.string(mandatory = True),
        "architecture": attr.string(mandatory = True),
        # control is only used to populate the sbom, see https://github.com/GoogleContainerTools/distroless/issues/1373
        # for why writing debian control files to the image is incompatible with scanners.
        "control": attr.label(),
    },
)

def _node_impl(module_ctx):
    mod = module_ctx.modules[0]

    if len(module_ctx.modules) > 1:
        fail("node.archive should be called only once")
    if not mod.is_root:
        fail("node.archive should be called from root module only.")

    # Node (https://nodejs.org/en/about/releases/)
    # Follow Node's maintainence schedule and support all LTS versions that are not end of life

    node_archive(
        name = "nodejs20_amd64",
        sha256 = "76272878069683c3a36b933d2f4842436a26b527daa930ae9346b477011ee2f3",
        strip_prefix = "node-v20.19.3-linux-x64/",
        urls = ["https://nodejs.org/dist/v20.19.3/node-v20.19.3-linux-x64.tar.gz"],
        version = "20.19.3",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm64",
        sha256 = "8e6939f63b736470bf2cbda596ab62393f26d9af9d7046d61270899880d4f149",
        strip_prefix = "node-v20.19.3-linux-arm64/",
        urls = ["https://nodejs.org/dist/v20.19.3/node-v20.19.3-linux-arm64.tar.gz"],
        version = "20.19.3",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_arm",
        sha256 = "c9cb3e73efacb8e3cce47f36b0fd0b823a84b973047f1ab58a5fce99bc6b7536",
        strip_prefix = "node-v20.19.3-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v20.19.3/node-v20.19.3-linux-armv7l.tar.gz"],
        version = "20.19.3",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_ppc64le",
        sha256 = "b38c429376c4aeee09e0e3ba3294db9e2683e896747b0050aa84ed3401706389",
        strip_prefix = "node-v20.19.3-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v20.19.3/node-v20.19.3-linux-ppc64le.tar.gz"],
        version = "20.19.3",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs20_s390x",
        sha256 = "d2ffe8fbc8b3f6eef0cb6ea3719c562d0c4535870e2a2d2f0685e7d2ae8b1c69",
        strip_prefix = "node-v20.19.3-linux-s390x/",
        urls = ["https://nodejs.org/dist/v20.19.3/node-v20.19.3-linux-s390x.tar.gz"],
        version = "20.19.3",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_amd64",
        sha256 = "0fa01328a0f3d10800623f7107fbcd654a60ec178fab1ef5b9779e94e0419e1a",
        strip_prefix = "node-v22.17.0-linux-x64/",
        urls = ["https://nodejs.org/dist/v22.17.0/node-v22.17.0-linux-x64.tar.gz"],
        version = "22.17.0",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm64",
        sha256 = "3e99df8b01b27dc8b334a2a30d1cd500442b3b0877d217b308fd61a9ccfc33d4",
        strip_prefix = "node-v22.17.0-linux-arm64/",
        urls = ["https://nodejs.org/dist/v22.17.0/node-v22.17.0-linux-arm64.tar.gz"],
        version = "22.17.0",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_arm",
        sha256 = "ce120efe921de3eaaba2394edaacfab3e61376a56199cb93fc7e9bf0b3f14a16",
        strip_prefix = "node-v22.17.0-linux-armv7l/",
        urls = ["https://nodejs.org/dist/v22.17.0/node-v22.17.0-linux-armv7l.tar.gz"],
        version = "22.17.0",
        architecture = "arm",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_ppc64le",
        sha256 = "c3a49ac3d56404290a66afdd9b149d87201fa1dc52740c21cf70b182cf8dfc4c",
        strip_prefix = "node-v22.17.0-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v22.17.0/node-v22.17.0-linux-ppc64le.tar.gz"],
        version = "22.17.0",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs22_s390x",
        sha256 = "394f839554bfd8c7e8db006a4de7604553bc478c26e6082749731f6d612c6f78",
        strip_prefix = "node-v22.17.0-linux-s390x/",
        urls = ["https://nodejs.org/dist/v22.17.0/node-v22.17.0-linux-s390x.tar.gz"],
        version = "22.17.0",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_amd64",
        sha256 = "292c187efeb7a360dee81efb519518dea2e02cb98e4c0d1d5a0f0af62c31e4b6",
        strip_prefix = "node-v24.4.0-linux-x64/",
        urls = ["https://nodejs.org/dist/v24.4.0/node-v24.4.0-linux-x64.tar.gz"],
        version = "24.4.0",
        architecture = "amd64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_arm64",
        sha256 = "637ab954900450101c9a254df9014771f5f21d5fd12fec53e4cf4f8bae7e7365",
        strip_prefix = "node-v24.4.0-linux-arm64/",
        urls = ["https://nodejs.org/dist/v24.4.0/node-v24.4.0-linux-arm64.tar.gz"],
        version = "24.4.0",
        architecture = "arm64",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_ppc64le",
        sha256 = "dd7bbd65b315097c685aef1f0f0cd4cb5e605a1a94458dc44cae7da814eb209a",
        strip_prefix = "node-v24.4.0-linux-ppc64le/",
        urls = ["https://nodejs.org/dist/v24.4.0/node-v24.4.0-linux-ppc64le.tar.gz"],
        version = "24.4.0",
        architecture = "ppc64le",
        control = "//nodejs:control",
    )

    node_archive(
        name = "nodejs24_s390x",
        sha256 = "efa22fc1bdb25c9eb656465b9304b02a037c2d07774c9f9adaf3bf3741583cd3",
        strip_prefix = "node-v24.4.0-linux-s390x/",
        urls = ["https://nodejs.org/dist/v24.4.0/node-v24.4.0-linux-s390x.tar.gz"],
        version = "24.4.0",
        architecture = "s390x",
        control = "//nodejs:control",
    )

    return module_ctx.extension_metadata(
        root_module_direct_deps = [
            "nodejs20_amd64",
            "nodejs20_arm64",
            "nodejs20_arm",
            "nodejs20_ppc64le",
            "nodejs20_s390x",
            "nodejs22_amd64",
            "nodejs22_arm64",
            "nodejs22_arm",
            "nodejs22_ppc64le",
            "nodejs22_s390x",
            "nodejs24_amd64",
            "nodejs24_arm64",
            "nodejs24_ppc64le",
            "nodejs24_s390x",
        ],
        root_module_direct_dev_deps = [],
    )

_archive = tag_class(attrs = {})

node = module_extension(
    implementation = _node_impl,
    tag_classes = {
        "archive": _archive,
    },
)
